<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">

    <title>Complex Refractions in WebGL (WIP)</title>
    <meta name="description" content="A technique for good-enough realtime refraction through forms with hollows.">
    <meta name="sort" content="1970-01-00">
</head>

<body>
<article>
<style>
    .webgl-holder {
        max-height: 720px;
        width: auto;
        aspect-ratio: 1;
        max-width: 100%;
    }
    .cube-map {
        width: 100%;
        display: flex;
        flex-wrap: wrap;
        gap: 3px;
    }
    .cube-map img {
        flex: 0 1 auto;
        margin: 0;
    }
</style>

    <script defer src="/article-data/complex-refraction/built.js"></script>

    <p>This demo and article are a work in progress. Have a look if you like!</p>

    <noscript>
        <p class="warning">Javascript is disabled or unavailable in your browser so the interactive demo can't be
            shown.</p>
    </noscript>

    <div id="simple-cube" class="webgl-holder"></div>

    <h2>Placeholder Summary</h2>
    <p>I'll write this up properly later, for now, a brief overview:</p>

    <p>This is rendered as a single simple rounded cuboid mesh. The interior hollow is defined by an axis-aligned bounding box that the fragment shader raycasts against. The new(?) idea here is using a cubemap to lookup the normals for both the interior and exterior surfaces. Unlike traditional UV mapping, this makes it far easier to go from a ray's position to what should be sampled.</p>

    <p>Similar to using simplified volumes for physics, this uses simplified volumes for the ray casts and then looks up detailed surface normal information from the cubemap.</p>

    <p>The more that the forms we raycast against deviate from the originally modelled form, the less accurate the result.</p>

    <h2>Vertex Shader</h2>
    <pre><code>varying vec3 vObjectPosition;
varying vec3 vObjectCameraPosition;
varying mat4 vModelMatrix;

void main() {
    vObjectPosition = position;

    gl_Position = projectionMatrix * modelViewMatrix * vec4(vObjectPosition.xyz, 1.0);

    vObjectCameraPosition = (vec4(cameraPosition.xyz, 1.0) * modelMatrix).xyz;

    vModelMatrix = modelMatrix;
}</code></pre>

    <h2>Fragment Shader</h2>
    <pre><code>uniform samplerCube environmentSampler;
uniform samplerCube interiorSampler;
uniform samplerCube exteriorSampler;

uniform float refractiveIndexOutside;
uniform float refractiveIndexInside;
uniform vec3 aabbExterior;
uniform vec3 aabbInterior;
uniform float absorbanceCoefficient;
uniform vec3 absorbanceColour;

varying vec3 vObjectPosition;
varying vec3 vObjectCameraPosition;
varying mat4 vModelMatrix;

// Will return (0,0,0) in case of total internal reflection.
vec3 applyRefraction(vec3 incoming, vec3 surfaceNormal, float indexA, float indexB) {
    // In reality refractive index should never be less than 1, but just in case.
    float ratio = abs(indexB) &lt; 0.001
        ? 1000.0
        : indexA / indexB;

    return refract(incoming, surfaceNormal, ratio);
}

// approximation of the Fresnel equation for reflectance traveling from medium A to B.
// based on https://graphics.stanford.edu/courses/cs148-10-summer/docs/2006--degreve--reflection_refraction.pdf
float schlickApproximation(vec3 incoming, vec3 surfaceNormal, float indexA, float indexB) {
    float cosX = abs(dot(surfaceNormal, incoming));
    if (abs(cosX) &lt; 0.001) {
        // Incoming ray is skimming the surface, so just reflect.
        return 1.0;
    }

    if (indexA &gt; indexB) {
        float n = indexA / indexB;
        float sinT2 = n * n * (1.0 - cosX * cosX);

        if (sinT2 &gt; 1.0)
        {
            // Total internal reflection.
            return 1.0;
        }

        cosX = sqrt(1.0 - sinT2);
    }

    float ratio = (indexA - indexB) / (indexA + indexB);
    float ratioSquared = ratio * ratio;
    float x = 1.0 - cosX;
    return ratioSquared + (1.0 - ratioSquared) * x*x*x*x*x;
}

void castThroughInterface(in vec3 incomingDirection, in vec3 surfaceNormal,
    in float indexA, in float indexB,
    out vec3 reflectDirection, out vec3 refractDirection, out float reflectance
) {
    reflectance = schlickApproximation(incomingDirection, surfaceNormal, indexA, indexB);
    reflectDirection = reflect(incomingDirection, surfaceNormal);
    refractDirection = refract(incomingDirection, surfaceNormal, indexA / indexB);
}

float max4(in float a, in float b, in float c, in float d) {
    return max(max(a, b), max(c, d));
}

float min4(in float a, in float b, in float c, in float d) {
    return min(min(a, b), min(c, d));
}

// tIntersect -1.0 if no intersection, otherwise the distance along the ray to the intersection.
void aabbIntersection(in vec3 rayDirection, in vec3 rayPosition,
    in vec3 aabbMin, in vec3 aabbMax,
    out float tIntersect
) {
    vec3 t1 = aabbMin - rayPosition / rayDirection;
    vec3 t2 = aabbMax - rayPosition / rayDirection;

    float minOffset = 0.00001;

    float tMin = max4(minOffset, min(t1.x, t2.x), min(t1.y, t2.y), min(t1.z, t2.z));
    float tMax = min4(1e12, max(t1.x, t2.x), max(t1.y, t2.y), max(t1.z, t2.z));

    if (tMin &lt;= minOffset && tMax &gt; tMin) {
        // Ray starts inside the AABB.
        tIntersect = tMax;
    } else {
        // Ray starts outside the AABB.
        tIntersect = ((tMin &lt;= minOffset) || (tMin &gt; tMax))
            ? -1.0
            : tMin;
    }
}

vec3 sampleEnvFromObjectDirection(in vec3 objectDirection, in mat4 modelMatrix, in samplerCube environmentSampler) {
    vec3 worldDirection = (modelMatrix * vec4(objectDirection.xyz, 0.0)).xyz;
    return textureCube(environmentSampler, worldDirection).rgb;
}

vec3 sampleNormal(in vec3 objectPosition, in samplerCube normalSampler) {
    vec3 raw = vec3(1.0) - textureCube(normalSampler, normalize(objectPosition)).rgb;
    vec3 normal = normalize(vec3(
        (raw.x - 0.5) * 2.0,
        (raw.y - 0.5) * 2.0,
        (raw.z - 0.5) * 2.0
    ));

    return normal;
}

void main() {
    vec3 aabbInternalMin = aabbInterior * -0.5;
    vec3 aabbInternalMax = aabbInterior * 0.5;

    vec3 aabbExternalMin = aabbExterior * -0.5;
    vec3 aabbExternalMax = aabbExterior * 0.5;

    float totalDistance = 0.0;

    // Ray enters the mesh at A.
    vec3 aNormal = sampleNormal(vObjectPosition.xyz, exteriorSampler).xyz;
    vec3 aPosition = vObjectPosition.xyz;

    // Handle interface at A.
    vec3 aRefractDirection = vec3(0.0, 0.0, 0.0);
    vec3 aReflectDirection = vec3(0.0, 0.0, 0.0);
    float aReflectance = 0.0;
    castThroughInterface(normalize(aPosition - vObjectCameraPosition), aNormal,
        refractiveIndexOutside, refractiveIndexInside,
        aReflectDirection, aRefractDirection, aReflectance
    );

    // Some is immediately reflected back out of the mesh.
    vec3 aColourReflect = sampleEnvFromObjectDirection(aReflectDirection, vModelMatrix, environmentSampler);

    // aRefracted may enter interior volume, if so it's at B.
    float bDistance = 0.0;
    aabbIntersection(aRefractDirection, aPosition,
        aabbInternalMin, aabbInternalMax,
        bDistance
    );

    totalDistance += max(0.0, bDistance);

    vec3 bPosition = bDistance &gt;= -0.5
        ? aPosition + aRefractDirection * bDistance
        : aPosition;

    vec3 beforeDPosition = vec3(0.0, 0.0, 0.0);
    vec3 dIncomingDirection = vec3(0.0, 0.0, 0.0);

    if (bDistance &lt; -0.5) {

        // Ray doesn't enter interior volume, so skip to it leaving.
        beforeDPosition = aPosition;
        dIncomingDirection = aRefractDirection;

    } else {

        // Find the normal of the interior at the point where the ray enters interior.
        vec3 bNormal = sampleNormal(bPosition, interiorSampler).xyz;

        // TODO: around here is where we'd start handling liquid filling.

        // Find how the ray gets split when entering interior space.
        vec3 bRefractDirection = vec3(0.0, 0.0, 0.0);
        vec3 bReflectDirection = vec3(0.0, 0.0, 0.0);
        float bReflectance = 0.0;
        castThroughInterface(aRefractDirection, bNormal,
            refractiveIndexInside, refractiveIndexOutside,
            bReflectDirection, bRefractDirection, bReflectance
        );

        if (bReflectance &gt;= 0.999) {

            // Total internal reflection, so just follow reflection ray.
            beforeDPosition = bPosition;
            dIncomingDirection = bReflectDirection;

        } else {

            // Find where the ray leaves the interior AABB.
            float cDistance = 0.0;
            aabbIntersection(bRefractDirection, bPosition,
                aabbInternalMin, aabbInternalMax,
                cDistance
            );

            // Don't add to totalDistance, as this is in the interior volume.

            vec3 cPosition = (cDistance &gt;= -0.5)
                ? (bPosition + bRefractDirection * cDistance)
                : bPosition;

            // Find the normal of the interior at the point where the ray exits.
            vec3 cNormal = sampleNormal(cPosition, interiorSampler).xyz;

            // Dump cNormal (useful for demo)
            // gl_FragColor = vec4((cNormal + 1.0) * 0.5, 1.0);
            // return;

            // Find how the ray gets split when leaving the interior space.
            vec3 cRefractDirection = vec3(0.0, 0.0, 0.0);
            vec3 cReflectDirection = vec3(0.0, 0.0, 0.0);
            float cReflectance = 0.0;
            castThroughInterface(bRefractDirection, cNormal * -1.0,
                refractiveIndexOutside, refractiveIndexInside,
                cReflectDirection, cRefractDirection, cReflectance
            );

            // Ignoring reflection here. Going from air to glass so this'll be minor.

            dIncomingDirection = cRefractDirection;
            beforeDPosition = cPosition;
        }
    }

    // Find where the ray exits the mesh.
    float dDistance = 0.0;
    aabbIntersection(dIncomingDirection, beforeDPosition,
        aabbExternalMin, aabbExternalMax,
        dDistance
    );

    totalDistance += max(0.0, dDistance);
    
    vec3 dPosition = (dDistance &gt;= 0.0)
        ? (beforeDPosition + dIncomingDirection * dDistance)
        : beforeDPosition;

    vec3 dNormal = sampleNormal(dPosition, exteriorSampler).xyz;

    // Find how the refracted ray gets split when leaving the mesh.
    vec3 dRefractDirection = vec3(0.0, 0.0, 0.0);
    vec3 dReflectDirection = vec3(0.0, 0.0, 0.0);
    float dReflectance = 0.0;

    castThroughInterface(dIncomingDirection, dNormal * -1.0,
        refractiveIndexInside, refractiveIndexOutside,
        dReflectDirection, dRefractDirection, dReflectance
    );

    // Some is refracted out into the environment.
    vec3 exitRefraction = sampleEnvFromObjectDirection(dRefractDirection, vModelMatrix, environmentSampler);

    // Some is reflected back into the AABB.
    // We *should* follow this through the AABB again, but we'll have to stop tracking at some point.
    vec3 exitReflection = sampleEnvFromObjectDirection(dReflectDirection, vModelMatrix, environmentSampler);

    vec3 interiorColour = mix(exitRefraction, exitReflection, dReflectance);

    // Dump distance (useful for demo)
    // totalDistance *= 8.0;
    // gl_FragColor.rgba = vec4(totalDistance, totalDistance, totalDistance, 1.0);
    // return;

    interiorColour = max(
        vec3(0.0, 0.0, 0.0),
        interiorColour * exp(
            -1.0 * totalDistance * absorbanceCoefficient * absorbanceColour
        )
    );

    gl_FragColor.rgba = vec4(mix(interiorColour, aColourReflect, aReflectance), 1.0);
}
    </code></pre>

    <h2>CubeMap</h2>
    <p>The baked normals of the internal volume, as a cubemap.</p>
    <div class="cube-map">
        <img src="/article-data/complex-refraction/bottle/interior-neg-x.png">
        <img src="/article-data/complex-refraction/bottle/interior-pos-x.png">
        <img src="/article-data/complex-refraction/bottle/interior-neg-y.png">
        <img src="/article-data/complex-refraction/bottle/interior-pos-y.png">
        <img src="/article-data/complex-refraction/bottle/interior-neg-z.png">
        <img src="/article-data/complex-refraction/bottle/interior-pos-z.png">
    </div>

    <p>Reference for ray vs AABB: <a href="https://tavianator.com/2022/ray_box_boundary.html" target="_blank">Ray vs AABB</a></p>

</article>
</body>
